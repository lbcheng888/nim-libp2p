plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.serialization) apply false
    alias(libs.plugins.kotlin.kapt) apply false
    alias(libs.plugins.kotlin.compose) apply false
}

// 解析 ABI 选择（支持 -Pabi=arm64-v8a 或 -Pabis=arm64-v8a,x86_64 或 target triple 名字）
ext.resolveSelectedAbis = {
    def abiProp = project.findProperty('abi') ?: project.findProperty('abis')
    def all = ['arm64-v8a','armeabi-v7a','x86_64']
    // 默认仅构建 arm64（华为/HarmonyOS 主流）。如需多 ABI，请通过 -Pabis 指定。
    if (!abiProp) return ['arm64-v8a']
    def items = abiProp.toString().split(/[;,\s]+/).collect { it.trim() }.findAll { it }
    def norm = items.collect { v ->
        switch(v) {
            case ['aarch64-linux-android','arm64','arm64-v8a']: return 'arm64-v8a'
            case ['armv7-linux-androideabi','armeabi-v7a','armv7']: return 'armeabi-v7a'
            case ['x86_64-linux-android','x86_64']: return 'x86_64'
            default: return v
        }
    }.unique()
    def valid = norm.findAll { all.contains(it) }
    return valid.isEmpty() ? all : valid
}

def SELECTED_ABIS = resolveSelectedAbis()

// 同步 Rust 生成的 .so 到 jniLibs，并可选调用脚本构建
tasks.register("syncNative") {
    group = "native"
    description = "Build Nim libp2p shared library and sync into app/src/main/jniLibs"

    doLast {
        SELECTED_ABIS.each { abi ->
            if (abi != 'arm64-v8a') {
                logger.warn("ABI ${abi} is not yet supported by the Nim build scripts")
            }
        }

        if (!SELECTED_ABIS.contains('arm64-v8a')) {
            logger.lifecycle("No supported ABI selected; skipping Nim build")
            return
        }

        def opensslCrypto = new File(rootDir, "build/openssl-android/android-arm64/lib/libcrypto.a")
        if (!opensslCrypto.exists()) {
            exec {
                workingDir rootDir
                commandLine "bash", "-lc", "scripts/build_openssl_android.sh 24"
            }
        }

        exec {
            workingDir rootDir
            commandLine "bash", "-lc", "scripts/build_nim_android.sh"
        }
    }
}

// Ensure native .so is synced before building APKs (unless -Pskip_native=true)
gradle.projectsEvaluated {
    def skipNative = project.hasProperty('skip_native') && project.property('skip_native').toString().toBoolean()
    if (!skipNative) {
        // Hook into app project tasks if possible, or root tasks that trigger app tasks
        project(":app").tasks.matching { it.name == 'preBuild' }.all { t ->
            t.dependsOn rootProject.tasks.named('syncNative')
        }
    } else {
        logger.lifecycle("skip_native=true -> do NOT depend on syncNative in preBuild")
    }
}

// Allow skipping native (CMake & Rust) in CI via -Pskip_native=true
if (project.hasProperty('skip_native') && project.property('skip_native').toString().toBoolean()) {
    gradle.projectsEvaluated {
        project(":app").tasks.matching { t ->
            t.name.startsWith('buildCMake') ||
            t.name.startsWith('configureCMake') ||
            t.name.contains('externalNativeBuild')
        }.configureEach { t ->
            t.enabled = false
            logger.lifecycle("Disabled native build task ${t.path} due to -Pskip_native")
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
